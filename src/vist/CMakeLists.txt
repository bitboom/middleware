#  Copyright (c) 2019 Samsung Electronics Co., Ltd All Rights Reserved
#
#  Licensed under the Apache License, Version 2.0 (the "License");
#  you may not use this file except in compliance with the License.
#  You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
#  Unless required by applicable law or agreed to in writing, software
#  distributed under the License is distributed on an "AS IS" BASIS,
#  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#  See the License for the specific language governing permissions and
#  limitations under the License

SET(TARGET_VIST_CLI vist-cli)
SET(TARGET_VIST_DAEMON vistd)
SET(TARGET_VIST_TEST vist-test)

SET(${TARGET_VIST_LIB}_SRCS "")
SET(${TARGET_VIST_LIB}_TESTS "")
SET(${TARGET_VIST_COMMON_LIB}_SRCS "")

IF(DEFINED GBS_BUILD)
	SET(DEPENDENCY sqlite3
				   dlog
				   gflags
				   libsystemd-daemon)
	PKG_CHECK_MODULES(VIST_COMMON_DEPS REQUIRED ${DEPENDENCY})
	INCLUDE_DIRECTORIES(${VIST_COMMON_DEPS_INCLUDE_DIRS})
	ADD_DEFINITIONS(-DTIZEN="TIZEN")
ENDIF(DEFINED GBS_BUILD)

INCLUDE_DIRECTORIES(SYSTEM . common ${VIST_COMMON_DEPS_INCLUDE_DIRS})

ADD_DEFINITIONS(-DDB_PATH="${DB_INSTALL_DIR}/.vist.db"
				-DDEFAULT_ADMIN_PATH="${DEFAULT_ADMIN_PATH}"
				-DPLUGIN_INSTALL_DIR="${PLUGIN_INSTALL_DIR}"
				-DSCRIPT_INSTALL_DIR="${SCRIPT_INSTALL_DIR}")

# common
ADD_SUBDIRECTORY(common)
ADD_SUBDIRECTORY(database)
ADD_SUBDIRECTORY(event)
ADD_SUBDIRECTORY(klass)
ADD_SUBDIRECTORY(logger)
ADD_SUBDIRECTORY(rmi)
ADD_SUBDIRECTORY(sdk)

# policy
ADD_SUBDIRECTORY(policy)

# client
ADD_SUBDIRECTORY(client)

# service
ADD_SUBDIRECTORY(service)
ADD_SUBDIRECTORY(notification)

ADD_LIBRARY(${TARGET_VIST_COMMON_LIB} STATIC ${${TARGET_VIST_COMMON_LIB}_SRCS})

IF(DEFINED GBS_BUILD)
	TARGET_LINK_LIBRARIES(${TARGET_VIST_COMMON_LIB} ${VIST_COMMON_DEPS_LIBRARIES}
													pthread glog gflags boost_system)
ELSE(DEFINED GBS_BUILD)
	TARGET_LINK_LIBRARIES(${TARGET_VIST_COMMON_LIB} pthread glog gflags boost_system)
ENDIF(DEFINED GBS_BUILD)

ADD_LIBRARY(${TARGET_VIST_LIB} STATIC ${${TARGET_VIST_LIB}_SRCS})
TARGET_LINK_LIBRARIES(${TARGET_VIST_LIB} ${TARGET_VIST_COMMON_LIB}
										 ${TARGET_VIST_POLICY_LIB}
										 ${TARGET_OSQUERY_LIB}
										 vist-rmi)

ADD_EXECUTABLE(${TARGET_VIST_DAEMON} main/main.cpp)
TARGET_LINK_LIBRARIES(${TARGET_VIST_DAEMON} ${TARGET_VIST_LIB})
TARGET_LINK_WHOLE(${TARGET_VIST_DAEMON} ${TARGET_OSQUERY_LIB})
SET_TARGET_PROPERTIES(${TARGET_VIST_DAEMON} PROPERTIES COMPILE_FLAGS "-fPIE")
SET_TARGET_PROPERTIES(${TARGET_VIST_DAEMON} PROPERTIES LINK_FLAGS "-pie")
INSTALL(TARGETS ${TARGET_VIST_DAEMON}
		DESTINATION ${CMAKE_INSTALL_BINDIR}
		PERMISSIONS OWNER_READ
					OWNER_WRITE
					OWNER_EXECUTE
					GROUP_READ
					GROUP_EXECUTE
					WORLD_READ
					WORLD_EXECUTE)

ADD_EXECUTABLE(${TARGET_VIST_CLI} main/cli.cpp)
TARGET_LINK_LIBRARIES(${TARGET_VIST_CLI} ${TARGET_VIST_CLIENT_LIB})
SET_TARGET_PROPERTIES(${TARGET_VIST_CLI} PROPERTIES COMPILE_FLAGS "-fPIE")
SET_TARGET_PROPERTIES(${TARGET_VIST_CLI} PROPERTIES LINK_FLAGS "-pie")
INSTALL(TARGETS ${TARGET_VIST_CLI}
		DESTINATION ${CMAKE_INSTALL_BINDIR}
		PERMISSIONS OWNER_READ
					OWNER_WRITE
					OWNER_EXECUTE
					GROUP_READ
					GROUP_EXECUTE
					WORLD_READ
					WORLD_EXECUTE)

ADD_EXECUTABLE(${TARGET_VIST_TEST} main/tests.cpp
								   ${${TARGET_VIST_LIB}_TESTS})
TARGET_LINK_LIBRARIES(${TARGET_VIST_TEST} ${TARGET_VIST_LIB}
										  ${TARGET_VIST_CLIENT_LIB}
										  ${TARGET_VIST_COMMON_LIB}
										  vist-rmi-static
										  gtest)
TARGET_LINK_WHOLE(${TARGET_VIST_TEST} ${TARGET_OSQUERY_LIB})
ADD_TEST(${TARGET_VIST_TEST} ${TARGET_VIST_TEST})
INSTALL(TARGETS ${TARGET_VIST_TEST}
		DESTINATION ${CMAKE_INSTALL_BINDIR}
		PERMISSIONS OWNER_READ
					OWNER_WRITE
					OWNER_EXECUTE
					GROUP_READ
					GROUP_EXECUTE
					WORLD_READ
					WORLD_EXECUTE)
